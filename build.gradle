plugins {
	id 'fabric-loom' version '1.+'
}

group = modGroup
archivesBaseName = modId
version = "$modVersion+$gameVersion"

repositories {
	maven { url 'https://api.modrinth.com/maven'; content { includeGroup 'maven.modrinth' } }
	maven { url 'https://maven.gegy.dev' }
	maven { url 'https://maven.shedaniel.me' }
	maven { url 'https://maven.terraformersmc.com' }
	maven { url 'https://ueaj.dev/maven' }
}

// See `runs.gradle`
apply from: 'runs.gradle', to: this

loom {
	mods {
		'simply-no-shading' {
			sourceSet 'main'
		}

		'simply-no-shading-test' {
			sourceSet 'test'
		}

		'simply-no-shading-bedrockify-test' {
			sourceSet 'bedrockifyTest'
		}

		'simply-no-shading-enhancedblockentities-test' {
			sourceSet 'enhancedblockentitiesTest'
		}

		'simply-no-shading-sodium-test' {
			sourceSet 'sodiumTest'
		}

		'simply-no-shading-indium-test' {
			sourceSet 'indiumTest'
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:$gameVersion"
	mappings loom.officialMojangMappings()
	modImplementation 'net.fabricmc:fabric-loader:latest.release'

	// See `dependencies.gradle`
	apply from: 'dependencies.gradle', to: dependencies
}

java {
	sourceCompatibility = javaVersion
	targetCompatibility = javaVersion

	withJavadocJar()
	withSourcesJar()
}

tasks.withType JavaCompile configureEach {
	options.release = project.javaVersion as int
}

tasks.named 'jar', {
	from 'LICENSE', {
		rename { "${it}_$project.archivesBaseName" }
	}

	manifest {
		attributes([
			'Implementation-Title': 'Simply No Shading',
			'Implementation-Version': archiveVersion,
			'Implementation-Vendor': 'StartsMercury'
		])
	}
}

tasks.named 'javadoc', {
	classpath += sourceSets.client.compileClasspath
	source += sourceSets.client.allJava

	options.addStringOption 'tag', 'implNote:a:Implementation Note:'
}

tasks.withType ProcessResources, {
	def usedPropertyKeys = [ 'gameVersion', 'javaVersion', 'modId', 'version' ]
	def usedProperties = usedPropertyKeys.collectEntries { [ String.valueOf(it), project.findProperty(it) ] }

	inputs.properties(usedProperties)

	filesMatching 'fabric.mod.json', {
		expand usedProperties
	}
}

// See `publishing.gradle`
apply from: 'publishing.gradle'
